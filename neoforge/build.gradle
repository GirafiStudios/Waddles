plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id 'net.darkhax.curseforgegradle' version '1.1.+'
    id "com.modrinth.minotaur" version "2.+"
}

base {
    archivesName = "${mod_name}-neoforge-${minecraft_version}"
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft_version
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each { variant ->
    configurations.named("$variant") {
        attributes {
            attribute(loaderAttribute, 'neoforge')
        }
    }
}
sourceSets.configureEach {
    [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName, it.getTaskName(null, 'jarJar')].each { variant->
        configurations.named("$variant") {
            attributes {
                attribute(loaderAttribute, 'neoforge')
            }
        }
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = System.getenv("curse_auth")

    var mainFile = upload(251767, jar)
    mainFile.changelogType = 'markdown'
    mainFile.changelog = rootProject.findProperty('mod_changelog')
    mainFile.addJavaVersion('Java 21')
    mainFile.releaseType = 'release'
    mainFile.addModLoader('NeoForge')
    def vr = rootProject.parseVersionRange(minecraft_version_range)
    mainFile.addGameVersion(*vr.quoted)
}

modrinth {
    var changelogText = rootProject.findProperty('mod_changelog')

    token = System.getenv("modrinth_auth")
    projectId = "LlRoNRWC"
    changelog = changelogText
    versionNumber = "${version}"
    versionType = "release"
    loaders = ["neoforge"]
    gameVersions = rootProject.parseVersionRange(minecraft_version_range).list
    uploadFile = jar
}
